# Copyright 2023 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("//build_overrides/pigweed.gni")

import("$dir_pw_build/target_types.gni")

config("default_config") {
  include_dirs = [ "public" ]
}

group("all") {
  deps = [
    ":app",
    ":game",
    ":snake",
  ]
}

pw_source_set("snake") {
  public_configs = [ ":default_config" ]
  public = [ "public/snake/snake.h" ]
  sources = [ "snake.cc" ]
  public_deps = [
    "$dir_pw_assert",
    "$dir_pw_chrono:system_clock",
    "$dir_pw_containers:inline_deque",
    "$dir_pw_function",
  ]
  deps = [ "$dir_pw_assert" ]
}

pw_source_set("game") {
  public_configs = [ ":default_config" ]
  public = [ "public/snake/game.h" ]
  sources = [ "game.cc" ]
  public_deps = [
    ":snake",
    "$dir_pwexperimental_color",
    "$dir_pwexperimental_framebuffer",
    "//lib/pw_touchscreen:buttons",
  ]
  deps = [
    "$dir_pw_assert",
    "$dir_pw_chrono:system_clock",
    "$dir_pw_log",
    "$dir_pw_sync:lock_annotations",
    "$dir_pw_sync:mutex",
    "$dir_pwexperimental_draw",
    "//lib/random",
  ]
}

pw_executable("app") {
  sources = [ "main.cc" ]
  deps = [
    ":game",
    "$dir_pw_chrono:system_clock",
    "$dir_pw_log",
    "$dir_pw_system:target_hooks",
    "$dir_pw_system:work_queue",
    "$dir_pw_thread:thread",
    "$dir_pwexperimental_display",
    "$dir_pwexperimental_framebuffer",
    "$pw_dir_third_party_32blit:32blit",
    "//applications/app_common",
    "//lib/framecounter",
    "//lib/pw_touchscreen:buttons",
  ]

  remove_configs = [ "$dir_pw_build:strict_warnings" ]
  if (host_os == "linux") {
    remove_configs += [ "$dir_pw_toolchain/host_clang:linux_sysroot" ]
  }

  if (pw_build_EXECUTABLE_TARGET_TYPE == "pico_executable") {
    ldflags = [ "-Wl,--print-memory-usage" ]
  }
}
